apply plugin: 'com.android.application'

static def gitRevision() {
    def cmd = "git rev-parse --short HEAD"
    return cmd.execute().text.trim()
}

static def gitDirty() {
    def cmd = "git diff-index --name-only HEAD"
    if(cmd.execute().text.trim().length()>0)
        return "-dirty";
    return "";
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"
    defaultConfig {
        applicationId "org.efidroid.efidroidmanager"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 5
        versionName "1.0-g${gitRevision()}${gitDirty()}"

        buildConfigField "long", "TIMESTAMP", System.currentTimeMillis() + "L"
        buildConfigField "String", "USERNAME", "\""+System.getProperty("user.name")+"\""
        buildConfigField "String", "HOSTNAME", "\""+InetAddress.getLocalHost().getHostName()+"\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-debug"
        }
    }
    externalNativeBuild {
        ndkBuild {
            path 'src/main/cpp/Android.mk'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile('com.github.afollestad.material-dialogs:core:0.8.5.6@aar') {
        transitive = true
    }
    compile('com.github.afollestad.material-dialogs:commons:0.8.5.6@aar') {
        transitive = true
    }
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.mcxiaoke.volley:library:1.0.19'
    compile 'org.ini4j:ini4j:0.5.4'
    compile 'commons-io:commons-io:2.5'
    compile 'com.melnykov:floatingactionbutton:1.3.0'
    compile project(':sub_projects:RootTools')
}
